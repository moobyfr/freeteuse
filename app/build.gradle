apply plugin: 'com.android.application'

def versionMajor = 1
def versionMinor = 9
def versionPatch = 16

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId    "bzh.leroux.yannick.freeteuse"
        minSdkVersion    23
        targetSdkVersion 28
        versionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            jniDebuggable true
        }
    }

    flavorDimensions "store"
    productFlavors {
        google {
            dimension "store"
            ext.extraPlugin = ""
        }
        huawei {
            dimension "store"
            versionNameSuffix "-hms"
            ext.extraPlugin = "com.huawei.agconnect"
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    ndkVersion '22.1.7171670'

    productFlavors.each { flavor ->
        if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains(flavor.name)) {
            if (flavor.ext.extraPlugin?.trim()) {
                apply plugin: flavor.ext.extraPlugin
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jmdns:jmdns:3.5.5'
    implementation 'org.slf4j:slf4j-android:1.7.7'

    huaweiImplementation 'com.huawei.agconnect:agconnect-crash:1.3.2.301'
    huaweiImplementation 'com.huawei.agconnect:agconnect-crash-native:1.3.2.301'
}
